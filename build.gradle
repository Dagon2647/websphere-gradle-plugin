apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: "idea"
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'bintray'


group = 'org.apimenov.gradle.plugins'
version = '0.0.6.SNAPSHOT'

sourceCompatibility = "1.5"
targetCompatibility = "1.5"

idea{
    module{
        languageLevel = sourceCompatibility
    }
}


buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}


repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'org.apache.commons:commons-lang3:3.3.2'

    testCompile(
        'org.spockframework:spock-core:0.6-groovy-1.8',
        'junit:junit:4.10')
}




task sourceJar(type: Jar) {
    from (sourceSets.main.allSource)
}

artifacts{
    archives  sourceJar
}



publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.12"
}

bintray {
    user = 'dagon2647'
    key = project.hasProperty("binTray.apiKey")?project.property("binTray.apiKey"):null
    //apiUrl = 'https://bintray.com/api/v1'
    publications = ["mavenJava"]
    // - OR -
    def pkgName = project.name


    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'maven'
        name = pkgName
        desc = 'Gradle plugin for websphere ant tasks'
        websiteUrl = 'https://github.com/Dagon2647/websphere-gradle-plugin/'
        issueTrackerUrl = 'https://github.com/Dagon2647/websphere-gradle-plugin/issues'
        vcsUrl = 'https://github.com/Dagon2647/websphere-gradle-plugin.git'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = false

    }

}

bintrayUpload.dependsOn assemble

bintrayUpload.onlyIf{
    project.hasProperty("binTray.apiKey")
}
